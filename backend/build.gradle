// Required gradle plugins.
plugins {
	id "org.springframework.boot" version "2.4.4"
	id "io.spring.dependency-management" version "1.0.11.RELEASE"
	id "io.snyk.gradle.plugin.snykplugin" version "0.4"
	id "org.sonarqube" version "3.1.1"
	id "jacoco"
	id "java"
	id "war"
}

// Snyk service attributes.
snyk {
	arguments = '--all-sub-projects'
	severity = 'high'
	autoDownload = true
	autoUpdate = true
}

// Sonarcloud service attributes.
sonarqube {
	properties {
		property "sonar.projectKey", "fvilarinho_demo_backend"
		property "sonar.organization", "fvilarinho"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.sources", "src/main/java,src/main/webapp,src/main/resources/static"
	}
}

// Sonarcloud task dependencies.
tasks.named("sonarqube").configure {
	dependsOn test
}

// Test coverage service attributes and dependencies.
jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
	}
}

// Test task dependencies.
test {
	finalizedBy jacocoTestReport
}

// Build attributes.
group = "br.com.demo"
version = "latest"
sourceCompatibility = "11"

// Default dependencies repository.
repositories {
	mavenCentral()
}

// List of dependencies.
dependencies {
	implementation "org.springframework.boot:spring-boot-starter-data-jpa"
	implementation "org.springframework.boot:spring-boot-starter-jersey"
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "jstl:jstl:1.2"
	implementation "org.mariadb.jdbc:mariadb-java-client:2.7.4"

	runtime "org.springframework.boot:spring-boot-starter-tomcat"

	testImplementation "org.mockito:mockito-core:3.12.4"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
}

// Default test platform.
test {
	useJUnitPlatform()
}